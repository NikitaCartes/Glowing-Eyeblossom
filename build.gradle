plugins {
    id "fabric-loom" version "1.8-SNAPSHOT"
    id ("com.modrinth.minotaur") version "2.+"
    id ("com.matthewprenger.cursegradle") version "1.4.0"
}

repositories {}

archivesBaseName = "${project.mod_id}-${project.minecraft_version}"
version = "${project.mod_version}"

sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = JavaVersion.VERSION_21


dependencies {
    // Fabric
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Hocon config
    include implementation("org.spongepowered:configurate-core:${hocon_version}")
    include implementation("org.spongepowered:configurate-hocon:${hocon_version}")
    include implementation("org.apache.commons:commons-text:${commons_text_version}")
    include implementation("com.typesafe:config:${typesafe_config_version}")
    include implementation("io.leangen.geantyref:geantyref:${geantyref_version}")

}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release.set(21)
}

java {
    withSourcesJar()
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

jar {
    from "LICENCE"
}

processResources {
    inputs.property "id", project.mod_id
    inputs.property "name", project.mod_name
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand(["id": project.mod_id, "name": project.mod_name, "version": project.version])
    }
}

modrinth {
    token.set(System.getenv("MODRINTH_TOKEN"))
    projectId.set("1S4LxcvL")
    versionNumber.set("${project.version}")
    versionName = "[1.21.2, 1.21.3] ${project.version}"
    versionType = "release"
    changelog.set("Release notes:\nhttps://github.com/NikitaCartes/Glowing-Torchflower/releases/tag/${project.version}")
    uploadFile = remapJar
    gameVersions.addAll("1.21.2", "1.21.3")
    loaders = ["fabric", "quilt"]
}

curseforge {
    apiKey = System.getenv("CURSEFORGE_TOKEN") ?: ""
    project {
        id = "827288"
        changelogType = "markdown"
        changelog = "Release notes:\nhttps://github.com/NikitaCartes/Glowing-Torchflower/releases/tag/${project.version}"
        releaseType = "release"

        addGameVersion("Fabric")
        addGameVersion("Quilt")

        addGameVersion("Java 21")

        addGameVersion("1.21.2")
        addGameVersion("1.21.3")

        mainArtifact(remapJar) {
            displayName = "[1.21.2, 1.21.3] ${project.version}"
        }
    }
    options {
        javaVersionAutoDetect = false
        javaIntegration = false
        forgeGradleIntegration = false
    }
}

tasks.register("publish") {
    dependsOn("modrinth")
    dependsOn("curseforge")
}